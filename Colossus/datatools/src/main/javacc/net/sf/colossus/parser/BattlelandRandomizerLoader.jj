options
{
  IGNORE_CASE = false;
  STATIC = false;
  LOOKAHEAD = 20;
  FORCE_LA_CHECK = true;
}


PARSER_BEGIN(BattlelandRandomizerLoader)
package net.sf.colossus.parser;

import java.util.*;
import java.util.logging.*;

import net.sf.colossus.variant.BattleHex;
import net.sf.colossus.variant.HazardTerrain;
import net.sf.colossus.variant.HazardHexside;

/**
 * BattlelandRandomizerLoader create a randomized Battleland from a description file.
 * @author Romain Dolbeau
 * @version $Id$
 * @see net.sf.colossus.variant.BattleHex
 */
public class BattlelandRandomizerLoader
{
    private static final boolean[][] show =
    {
        {false,false,true,true,true,false},
        {false,true,true,true,true,false},
        {false,true,true,true,true,true},
        {true,true,true,true,true,true},
        {false,true,true,true,true,true},
        {false,true,true,true,true,false}
    };
    private static final Random rand = new net.sf.colossus.util.DevRandom();

    private List<address> leftover;
    private List<address> usedup;

    BattleHex[][] hexes;

    private String title = "Randomized!";
    private String subtitle = "Randomized!";

    public String getTitle() { return title; }
    public String getSubtitle() { return subtitle; }

    Hashtable<String,List<address>> labels = new Hashtable<String,List<address>>();

    /* code executed just before the constructor call */
    /* used to add the predefined labels to the Hashtable */
    {
        int i,j;
        List<address> al;

        al = new ArrayList<address>();
        
        for (i = 1; i <= 4 ; i++)
            for (j = 2; j <= 3 ; j++)
                al.add(new address(i,j));
        al.add(new address(3,1));
        for (i = 2; i <= 4 ; i++)
            al.add(new address(i,4));
        labels.put("inside", al);

        al = new ArrayList<address>();

        for (i = 2; i <= 4 ; i++)
            al.add(new address(0,i));
        labels.put("leftdefenseentry", al);

        al = new ArrayList<address>();

        al.add(new address(3,0));
        for (i = 4; i <= 5 ; i++)
            al.add(new address(i,1));
        labels.put("upperdefenseentry", al);
        
        al = new ArrayList<address>();

        al.add(new address(5,4));
        for (i = 3; i <= 4 ; i++)
            al.add(new address(i,5));
        labels.put("lowerdefenseentry", al);

        al = new ArrayList<address>();

        for (i = 0; i < 6 ; i++)
            for (j = 0; j < 6 ; j++)
                if (show[i][j])
                    al.add(new address(i,j));
        labels.put("anywhere", al);
    }

    private class randomHazard implements Comparable<randomHazard>
    {
        private final String terrain;
        private final double prob;
        private final double[] e_prob = new double[3];

        private double percent(double val)
        {
            if (val < 0.)
                return 0.;
            if (val > 100.)
                return 100.;
            return val;
        }
        private double percent(double val, double sum)
        {
            return ((val * 100.) / sum);
        }
        randomHazard(String t)
        {
            terrain = t;
            prob = 100.;
            e_prob[0] = 100.;
            e_prob[1] = 0.;
            e_prob[2] = 0.;
        }
        randomHazard(String t, double p, int e)
        {
            terrain = t;
            prob = percent(p);
            e_prob[0] = 0.;
            e_prob[1] = 0.;
            e_prob[2] = 0.;
            if ((e < 1) || (e > 2))
                e_prob[0] = 100.;
            else
                e_prob[e] = 100.;
        }
        randomHazard(String t, double p, double p1, double p2, double p3)
        {
            double sum = percent(p1)+percent(p2)+percent(p3);
            terrain = t;
            prob = percent(p);
            e_prob[0] = percent(p1,sum);
            e_prob[1] = percent(p2,sum);
            e_prob[2] = percent(p3,sum);
        }
        public String toString()
        {
            String s =  "Hazard '" + terrain + "'";
            if (prob < 100.)
                s = s + " (" + prob + "%)";
            s = s + " [" + e_prob[0] + "/" + e_prob[1] + "/" + e_prob[2] + "]";
            return s;
        }
        public int compareTo(randomHazard other)
        {
            if (other.prob != prob)
            {
                return (prob > other.prob ? -1 : 1);
            }
            return 0;
        }
    }
    private class address implements Comparable<address>
    {
        final int x;
        final int y;
        address(int ox, int oy)
        {
            x = ox; y = oy;
        }
        public String toString()
        {
            return "(" + x + "," + y + ")";
        }
        public String toCaseLabel()
        {
            char xc = 'X';char yc = 'X';
            switch (x)
            {
            case 0:
                xc = 'A';
                yc = Integer.toString(5 - y).charAt(0);
                break;
            case 1:
                xc = 'B';
                yc = Integer.toString(5 - y).charAt(0);
                break;
            case 2:
                xc = 'C';
                yc = Integer.toString(6 - y).charAt(0);
                break;
            case 3:
                xc = 'D';
                yc = Integer.toString(6 - y).charAt(0);
                break;
            case 4:
                xc = 'E';
                yc = Integer.toString(6 - y).charAt(0);
                break;
            case 5:
                xc = 'F';
                yc = Integer.toString(5 - y).charAt(0);
                break;
            }
            return (xc + "" + yc);
        }
        public int compareTo(address other)
        {
            if (other.x != x)
            {
                return (x < other.x ? -1 : 1);
            }
            if (other.y != y)
            {
                return (y < other.y ? -1 : 1);
            }
            return 0;
        }
        public boolean equals(Object o)
        {
            if (o == null)
                return false;
            if (!(o instanceof address))
                return false;
            address ot = (address)o;
            return ((x == ot.x) && (y == ot.y));
        }
    }
    private boolean exist(address a)
    {
        return (a.x >= 0) && (a.x < show.length) &&
               (a.y >= 0) && (a.y < show[0].length) &&
               (show[a.x][a.y]);
    }
    private class range
    {
        final int min;
        final int max;
        range(int mi, int ma)
        {
            min = mi; max = ma;
        }
    }
    private void resolveOne(BattleHex[][] h, List<address> al, randomHazard t)
    {
        if (al.isEmpty())
            return;
        double result = rand.nextDouble() * 100.;
        if (result >= t.prob)
            return;
        int pos = rand.nextInt(al.size());
        address a = (address)al.remove(pos);
        BattleHex hex = h[a.x][a.y];
        hex.setTerrain(HazardTerrain.getTerrainByName(t.terrain));
        result = rand.nextDouble() * 100.;
        hex.setElevation(0);
        if (result > t.e_prob[0])
            hex.setElevation(1);
        if (result > (t.e_prob[0] + t.e_prob[1]))
            hex.setElevation(2);
        usedup.add(a);
    }
    
    private void resolveAll(BattleHex[][] h, List<address> al, List<randomHazard> tl)
    {
        usedup = new ArrayList<address>();
        Iterator<randomHazard> it = tl.iterator();
        while (it.hasNext())
        {
            randomHazard rt = it.next();
            resolveOne(h, al, rt);
        }
        leftover = al;
    }

    private Map<hazardPair,List<randomHexside>> hexsideRandomness = new TreeMap<hazardPair,List<randomHexside>>();
    private class hazardPair implements Comparable
    {
        final String hs;
        final int  es;
        final String hd;
        final int  ed;
        private hazardPair(String hs, int es, String hd, int ed)
        {
            this.hs = hs;
            this.es = es;
            this.hd = hd;
            this.ed = ed;
        }
        public boolean equals(Object o)
        {
            if (o == null)
                return false;
            if (!(o instanceof hazardPair))
                return false;
            hazardPair h = (hazardPair)o;
            return ((hs == h.hs) &&
                    (hd == h.hd) &&
                    (es == h.es) &&
                    (ed == h.ed));
        }
        public int compareTo(Object o)
        {
            hazardPair h = (hazardPair)o;
            if ((hs == h.hs) &&
                (hd == h.hd) &&
                (es == h.es) &&
                (ed == h.ed))
                return 0;
            if (!hs.equals(h.hs))
                return (hs.compareTo(h.hs));
            if (!hd.equals(h.hd))
                return (hd.compareTo(h.hd));
            if (es != h.es)
                return (es < h.es ? -1 : 1);
            if (ed != h.ed)
                return (ed < h.ed ? -1 : 1);
            return 0;
        }
        public String toString()
        {
            return("Pair: " + hs + "/" + (es == -1 ? "*" : ""+es) + " to " +
                              hd + "/" + (ed == -1 ? "*" : ""+ed));
        }
    }
    private class randomHexside
    {
        private final HazardHexside hazard;
        private final double prob;
        private double percent(double val)
        {
            if (val < 0.)
                return 0.;
            if (val > 100.)
                return 100.;
            return val;
        }
        randomHexside(char hChar, double p)
        {
        	HazardHexside hObj = HazardHexside.getHexsideByCode(hChar);
            hazard = hObj;
            prob = percent(p);
        }
        HazardHexside getHexsideHazard()
        {
            return hazard;
        }
        double getProb()
        {
            return prob;
        }
        public String toString()
        {
            return("HexsideHazard " + hazard + " " + (prob < 100. ?
                                                      prob + " %" :
                                                      ""));
        }
    }
    
    private List<randomHexside> findHazardPairBestMatch(hazardPair p)
    {
        hazardPair p2;
        List<randomHexside> al;
        p2 = p;
        al = hexsideRandomness.get(p2);
        if (al != null) // exact match
            return al;
        p2 = new hazardPair(p.hs, p.es, p.hd, -1);
        al = hexsideRandomness.get(p2);
        if (al != null)
            return al;
        p2 = new hazardPair(p.hs, -1, p.hd, p.ed);
        al = hexsideRandomness.get(p2);
        if (al != null)
            return al;
        p2 = new hazardPair(p.hs, -1, p.hd, -1);
        al = hexsideRandomness.get(p2);
        if (al != null)
            return al;
        return null;
    }
    
    public void resolveAllHexsides(BattleHex h[][])
    {
        for (int i = 0; i < 6; i++)
        {
            for (int j = 0; j < 6; j++)
            {
                if (show[i][j])
                {
                    for (int k = 0; k < 6; k++)
                    {
                        BattleHex hd = h[i][j].getNeighbor(k);
                        if (hd != null)
                        {
                            String ts = h[i][j].getTerrain().getName();
                            int  es = h[i][j].getElevation();
                            String td = hd.getTerrain().getName();
                            int  ed = hd.getElevation();
                            hazardPair hp = new hazardPair(ts,es,td,ed);
                            List<randomHexside> hsl = findHazardPairBestMatch(hp);
                            if (hsl != null)
                            {
                                boolean attributed = false;
                                Iterator<randomHexside> it = hsl.iterator();
                                while (!attributed && it.hasNext())
                                {
                                    randomHexside rhs = (randomHexside)it.next();
                                    if ((rand.nextDouble() * 100.) <
                                        rhs.getProb())
                                    {
                                        attributed = true;
                                        h[i][j].setHexsideHazard(k,rhs.getHexsideHazard());
                                    }
                                }
                            }
                        }
                    }
                }
            }
        }
        if (startlistAddress != null)
        {
            startlist = new java.util.ArrayList<String>();
            java.util.Iterator<address> it = startlistAddress.iterator();
            while (it.hasNext())
            {
                address ad = (address)it.next();
                startlist.add(ad.toCaseLabel());
            }
        }
    }

    /** hold the list of label for the startlist */
    private java.util.List<String> startlist = null;

    /** hold the list of address prior resolution for the startlist */
    private java.util.List<address> startlistAddress = null;
    
    /** is the terrain a Tower ? */
    private boolean isTower = false;
    
    public java.util.List<String> getStartList()
    {
        return startlist;
    }
    
    public boolean isTower()
    {
        return isTower;
    }
}

PARSER_END(BattlelandRandomizerLoader)

SKIP :
{
        " "
  |     "\r"
  |     "\t"
}

TOKEN :
{
        < COMMENT : "#"(<NOTNEWLINE>)*"\r" | "#"(<NOTNEWLINE>)*"\n" >
|       < #NOTNEWLINE : ~["\n","\r"] >
}

String z_comment() :
{}
{
    <COMMENT>
    {
        return(new String(token.image));
    }
}

TOKEN :
{
    < EOL: "\n" >
|   < AREA : "AREA" >
|   < HAZARDS : "HAZARDS" >
|   < KEYLABEL : "LABEL" >
|   < SOMEOF : "SOMEOF" >
|   < SUBSTRACT : "SUBSTRACT" >
|   < SURROUNDINGSOF : "SURROUNDINGSOF" >
|   < PAIR : "PAIR" >
|   < HEXSIDES : "HEXSIDES" >
|   < LEFTOVER : "leftover" >
|   < USEDUP : "usedup" >
|   < STARTLIST: "STARTLIST" >
|   < TOWER: "TOWER" >
|   < KEYTITLE : "TITLE" >
|   < KEYSUBTITLE : "SUBTITLE" >
}

TOKEN :
{
    < NUMBER : (<DIGIT>)+ >
|   < FPNUMBER : (<DIGIT>)+<DOT>(<DIGIT>)* >
|   < #DIGIT : ["0" - "9"] >
|   < COMMA : "," >
|   < DOT : "." >
|   < OPENPAR : "(" >
|   < CLOSEPAR : ")" >
|   < CARET : "-" >
|   < EQUAL : "=" >
|   < STAR : "*" >
}

TOKEN :
{
    < CASELABEL : ["A"-"F","a"-"f"]["1"-"6"] >
|   < HAZARD : ["a"-"z","A"-"Z"] >
}

TOKEN :
{
    < CHAINE : <CAR> (<NUMCAR>)* >
|   < QUOTEDCHAINE : "\"" (<QUOTEDCAR>)+ "\"" >
|   < #CAR : ["a"-"z","A"-"Z",".","_"] >
|   < #NUMCAR : <CAR>|["0"-"9"] >
|   < #QUOTEDCAR : <NUMCAR>|[" ","!","?",";",",",":","/","+","="] >
}

char z_terrain() :
{}
{
    <HAZARD>
    { return(token.image.charAt(0)); }
}

String z_label() :
{}
{
    <CHAINE>
    { return new String(token.image); }
}

int z_number() :
{}
{
    <NUMBER>
    { return(Integer.parseInt(token.image)); }
|   <STAR>
    { return((int)-1); }
}

double z_fpnumber() :
{}
{
    <FPNUMBER>
    { return(Double.parseDouble(token.image)); }
|   <STAR>
    { return((double)-1); }
}

String z_chaine() :
{}
{
    <CHAINE>
    {
        return(new String(token.image));
    }
|   <QUOTEDCHAINE>
    {
        String tok = new String(token.image);
        String cha = tok.substring(1, tok.length() - 1);
        return(cha);
    }
}

address z_address() :
{
    int x,y;
    List<address> al;
}
{
    <OPENPAR> (x = z_number()) <COMMA> (y = z_number()) <CLOSEPAR>
    {
        return new address(x,y);
    }
|   <CASELABEL>
    {
        switch (token.image.charAt(0))
        {
        case 'A':
        case 'a':
            x = 0;
            break;
        case 'B':
        case 'b':
            x = 1;
            break;
        case 'C':
        case 'c':
            x = 2;
            break;
        case 'D':
        case 'd':
            x = 3;
            break;
        case 'E':
        case 'e':
            x = 4;
            break;
        case 'F':
        case 'f':
            x = 5;
            break;
        default:
            throw new ParseException("Hex " + token.image + "doesn't exist");
        }
        y = 6 - Integer.parseInt(new String(token.image.substring(1)));
        y = y - (int)Math.abs(((x - 3) / 2));
        return new address(x,y);
    }
}

range z_range() :
{
    int a,b;
}
{
    (a = z_number()) <CARET> (b = z_number())
    { return new range(a,b); }
}

List<address> z_listAddress() :
{
    address ad;
    List<address> al, al2, al3;
    int x,y;
    range r1, r2;
    int i,j;
    String s;
}
{
    <SOMEOF> <OPENPAR> (x = z_number()) <COMMA> (al2 = z_listAddress()) <CLOSEPAR> (al = z_listAddress())
    {
        List<address> al4 = new ArrayList<address>(al);
        while (!al2.isEmpty() && (x > 0))
        {
            ad = (address)al2.remove(rand.nextInt(al2.size()));
            al4.add(ad);
            x--;
        }
        return al4;
    }
|   <SUBSTRACT> <OPENPAR> (al3 = z_listAddress()) <COMMA> (al2 = z_listAddress()) <CLOSEPAR> (al = z_listAddress())
    {
        List<address> al4 = new ArrayList<address>(al);
        Iterator<address> it = al3.iterator();
        while (it.hasNext())
        {
            ad = (address)it.next();
            if (!(al2.contains(ad)))
            {
                al4.add(ad);
            }
        }
        return al4;
    }
|   <OPENPAR> <LEFTOVER> <CLOSEPAR> (al = z_listAddress())
    {
        List<address> al4 = new ArrayList<address>(al);
        if (leftover != null)
        {
            Iterator<address> it = leftover.iterator();
            while (it.hasNext())
                al4.add(it.next());
        }
        return al4;
    }
|   <SURROUNDINGSOF> <OPENPAR> (al2 = z_listAddress()) <CLOSEPAR> (al = z_listAddress())
     {
        List<address> al4 = new ArrayList<address>(al);
        if (al2 != null)
        {
            Set<address> tempSet = new TreeSet<address>();
            Iterator<address> it = al2.iterator();
            while (it.hasNext())
            {
                ad = (address)it.next();
                for (i = 0; i < 6 ; i++)
                {
                    BattleHex hex = hexes[ad.x][ad.y].getNeighbor(i);
                    if (hex != null)
                    {
                        tempSet.add(new address(hex.getXCoord(),
                                                hex.getYCoord()));
                    }
                }
            }
            it = al2.iterator();
            while (it.hasNext())
            {
                ad = (address)it.next();
                tempSet.remove(ad);
            }
            it = tempSet.iterator();
            while (it.hasNext())
            {
                ad = (address)it.next();
                al4.add(ad);
            }
        }
        return al4;
    }
|   <OPENPAR> <USEDUP> <CLOSEPAR> (al = z_listAddress())
    {
        List<address> al4 = new ArrayList<address>(al);
        if (usedup != null)
        {
            Iterator<address> it = usedup.iterator();
            while (it.hasNext())
                al4.add(it.next());
        }
        return al4;
    }
|   <OPENPAR> (s = z_label()) <CLOSEPAR> (al = z_listAddress())
    {
        List<address> al4 = new ArrayList<address>(al);
        List<address> ll = labels.get(s);
        if (ll != null)
        {
            Iterator<address> it = ll.iterator();
            while (it.hasNext())
                al4.add(it.next());
        }
        return al4;
    }
|   (ad = z_address()) (al = z_listAddress())
    {
        if (exist(ad))
            al.add(ad);
        return(al);
    }
|   <OPENPAR> (r1 = z_range()) <COMMA> (y = z_number()) <CLOSEPAR> (al = z_listAddress())
    {
        for (i = r1.min ; i <= r1.max ; i++)
        {
            ad = new address(i,y);
            if (exist(ad))
                al.add(ad);
        }
        return al;
    }
|   <OPENPAR> (x = z_number()) <COMMA> (r2 = z_range()) <CLOSEPAR> (al = z_listAddress())
    {
        for (i = r2.min ; i <= r2.max ; i++)
        {
            ad = new address(x,i);
            if (exist(ad))
                al.add(ad);
        }
        return al;
    }
|   <OPENPAR> (r1 = z_range()) <COMMA> (r2 = z_range()) <CLOSEPAR> (al = z_listAddress())
    {
        for (i = r1.min ; i <= r1.max ; i++)
        for (j = r2.min ; j <= r2.max ; j++)
        {
            ad = new address(i,j);
            if (exist(ad))
                al.add(ad);
        }
        return al;
    }
|   /* empty */
    { return new ArrayList<address>(); }
}

List<randomHazard> z_listHazard() :
{
    String t;
    int e = 0;
    List<randomHazard> tl;
    double p = 100.0,p1,p2,p3;
}
{
    (t = z_chaine()) (<COMMA> (p = z_fpnumber()))? <COMMA> (p1 = z_fpnumber()) <COMMA> (p2 = z_fpnumber()) <COMMA> (p3 = z_fpnumber()) (tl = z_listHazard())
    {
        randomHazard rt = new randomHazard(t, p, p1, p2, p3);
        tl.add(rt);
        return tl;
    }
|   (t = z_chaine()) (<COMMA> (p = z_fpnumber()))? (<COMMA> (e = z_number()))? (tl = z_listHazard())
    {
        randomHazard rt = new randomHazard(t, p, e);
        tl.add(rt);
        return tl;
    }
|   /* empty */
    { return new ArrayList<randomHazard>(); }
}

List<randomHexside> z_listHexside() :
{
    char t;
    int e = 0;
    List<randomHexside> hl;
    double p = 100.0,p1,p2,p3;
}
{
    (t = z_terrain()) (<COMMA> (p = z_fpnumber()))? (hl = z_listHexside())
    {
        randomHexside rh = new randomHexside(t, p);
        hl.add(rh);
        return hl;
    }
|   /* empty */
    { return new ArrayList<randomHexside>(); }
}

hazardPair z_hazardPair() :
{
    String ts, td;
    int  es, ed;
}
{
    <OPENPAR> (ts = z_chaine()) <COMMA> (es = z_number()) <CLOSEPAR> <OPENPAR> (td = z_chaine()) <COMMA> (ed = z_number()) <CLOSEPAR>
    {
        return new hazardPair(ts,es,td,ed);
    }
}

int oneArea(BattleHex h[][]) :
{
    List<address> al;
    List<randomHazard> tl;
    List<randomHexside> tl2;
    String s;
    hexes = h;
    hazardPair hp;
}
{
    <AREA> (al = z_listAddress()) <HAZARDS> (tl = z_listHazard())
    {
        Collections.sort(al);
        Collections.reverse(tl);
        //System.err.println("Using area: " + al + ", terrains: " + tl);
        resolveAll(h, al, tl);
        //System.err.println("Usedup: " + usedup);
        //System.err.println("Leftover: " + leftover);
        return(1);
    }
|   <KEYLABEL> (s = z_label()) <EQUAL> (al = z_listAddress())
    {
        labels.put(s, al);
        return(0);
    }
|   <KEYTITLE> (s = z_chaine())
    {
        title = s;
        return(0);
    }
|   <KEYSUBTITLE> (s = z_chaine())
    {
        subtitle = s;
        return(0);
    }
|   <PAIR> (hp = z_hazardPair()) <HEXSIDES> (tl2 = z_listHexside())
    {
        //System.out.println("For " + hp + " we'll use " + tl2);
        Collections.reverse(tl2);
        hexsideRandomness.put(hp,tl2);
        //System.out.println("NOW: " + hexsideRandomness);
        return(0);
    }
|   <TOWER>
    {
        isTower = true;
        return 0;
    }
|   <STARTLIST> (al = z_listAddress())
    {
        startlistAddress = new java.util.ArrayList<address>(al);
        return 0;
    }
|   <EOL>
    {
        return(0);
    }
|   (s = z_comment())
    {
        return(0);
    }
|   <EOF>
    {
        return(-1);
    }
}
